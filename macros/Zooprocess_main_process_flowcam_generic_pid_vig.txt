// --------------------- Zooprocess_main_process_flowcam_generic_pid_vig.txt ------------------------------
version = "8.27";
date = "2024/08/31";

args = 	getArgument();
arg = 	split(args," ");
path  = 	arg[0];
instrum  = 	arg[1];
chem  = 	arg[2];
text_run = chem+"batch_run.txt";
text_stop = chem+"batch_stop.txt";
long = lengthOf(path);
cruise = substring(path,3,long-1);
pathraw = 	path+"\\raw\\";
pathconfig = path+"\\config\\";
processpid = true;
configfile = "no";
nb_pid_wo_tsv = 0;
advanced_mode = false;

// ---------------- Gestion STOP ------------------------------------
if (File.exists(text_run) == false && File.exists(text_stop) == false) {
	// -------------- Creation du fichier BATCH ---------------------
	if (isOpen("Log")) {	selectWindow("Log");	run("Close");	} // if
	print("DO NOT DELETE !!!");
	sauve = "save=" + text_run;
	selectWindow("Log");
	run("Text...",sauve);
	run('Close');
} // if

// ============================= USER / ADVANCED =============================
mode_user = 	pathconfig+"\\mode_file_user.txt";
mode_advanced = pathconfig+"\\mode_file_advanced.txt";

// ----------------- si aucun fichier de mode ----------------
if (File.exists(mode_advanced)) { advanced_mode = true;	} // if	

//--------------- Selection des fichiers META si plus d'un -------------------
filelist  = 	getFileList(path+"\\meta\\");
j = 0;
list = newArray(filelist.length);
for (i=0; i<filelist.length ; i++) {	
	ctrl = endsWith(filelist[i],".txt");
	ctrl1 = startsWith(filelist[i],"Flowcam_header_");
	ctrl2 = startsWith(filelist[i],"Generic_header_");
	if (ctrl == 1 && (ctrl1 == 1 || ctrl2 == 1))	{rr = filelist[i]; 	list[j] = rr;	j++;		} // if
} // for	
if (j==0) { 
	getBoolean("No header file in the "+path+"\\meta folder of the project. \n \nPress YES to end this process and fill metadata then !          ");
} //
else if (j==1) {
	// ----------------- Un seul fichier selectionne tout seul ----------------------------------------
	listaff = newArray(j);
	for (i=0; i<listaff.length ; i++) {	rr = list[i];		listaff[i] = rr;	} // for
	metafile = listaff[0];	
} 
else { 	// ---------------------Choix manuel du fichier meta -------------------------------------
	listaff = newArray(j);
	for (i=0; i<listaff.length ; i++) {	rr = list[i];		listaff[i] = rr;	} // for
	Dialog.create("METADATA FILE          version : "+version);
	Dialog.addMessage("--------------------------------------------------------------------------------   ");
	Dialog.addMessage("Project :  "+path);
	Dialog.addMessage("--------------------------------------------------------------------------------   ");
	message = "SELECT Metadata FILE             " ;
	Dialog.addMessage(message);
	Dialog.addChoice("       ", listaff);
	Dialog.show();
	metafile = Dialog.getChoice();
} // else

//------------------------------------- Fichier CONFIG (SAUF FLOWCAM COMPOSITE) ------------------------------------------------------------------
message  = "NO CONFIGURATION DATA for COMPOSITE images !";
edit = true;
while (edit == true) {
	if (indexOf(path,"flowcam_composite") < 0) {
		filelist  = 	getFileList(pathconfig);
		j = 0;
		list = newArray(filelist.length);
		for (i=0; i<filelist.length ; i++) {	
			ctrl = indexOf(filelist[i],"config");
			ctrl1 = endsWith(filelist[i],".txt");
			if (ctrl>0 && ctrl1==1 )		{rr = filelist[i]; 	list[j] = rr;	j++;		} // if
		} // for	
		//---------------------Test de la presence de fichiers dans le repertoire-----------------------------------------------------------------------	
		if (j==0 ) {
			getBoolean("No valid config file in "+pathconfig+". Press Cancel to abort.           ");
		} // if
		if (j==1 ) {		configfile = list[0];		} // if
		else if (j==1) {
			// ----------------- Un seul fichier selectionne tout seul ----------------------------------------
			listaff = newArray(j);                                                                                                                                          
			for (i=0; i<listaff.length ; i++) {	rr = list[i];		listaff[i] = rr;	} // for
			configfile = listaff[0];
		} // else if
		else {
			listaff = newArray(j);
			for (i=0; i<listaff.length ; i++) {	rr = list[i];		listaff[i] = rr;	} // for
			Dialog.create("SELECT PROCESS CONFIG FILE  ");
			Dialog.addMessage("--------------------------------------------------------------------------------------------------------------------------------------      ");
			Dialog.addMessage("Instrument :  "+instrum);
			Dialog.addMessage("Project :        "+path);
			Dialog.addMessage("--------------------------------------------------------------------------------------------------------------------------------------      ");
			message = "SELECT PROCESS CONFIG FILE from the list.             " ;
			Dialog.addMessage(message);
			Dialog.addChoice("       ", listaff);
			Dialog.show();
			configfile = Dialog.getChoice();
		}// else
		// ---------------------------- Lecture fichier existant -----------------------------------------------	
		openconfig = pathconfig +"\\"+configfile;
		open(openconfig);
		wait(100);
		config = getInfo();
		run("Close");
		conf  = split(config,"\n");
		size = conf.length;
		array = newArray(size);
		message= " ";
		for (i=0; i<conf.length; i++)	{ texte = conf[i];
			// ---------------- On n'affiche pas les champs pixels -----------
			if (indexOf(texte,"pixel") != 0) message = message+"\n"+texte; 	
			ssarray = split(texte," ");	
			array[i] = ssarray[1];		
		} // for
		if (instrum == "Flowcam") {
			calibration = 			array[0];
			background_method= 		array[1];
			nb_image= 			array[2];
			rolling= 			array[3];
			pixel4x= 			array[4];
			pixel10x= 			array[5];
			upper= 			array[6];
			gamma= 			array[7];
			echelle_micro=			array[8];
			process_option=		array[9];
			esdmin= 			array[10];
			esdmax= 			array[11];
			lut_offset= 			array[12];
			lut_slope= 			array[13];
			stop_after_n_images= 	array[14];
			stop_after_m_objects= 	array[15];
			scale= 				array[16];
			remove_objects_on_sides= 	array[17];
		} // if
		if (instrum == "Generic") {
			calibration = 			array[0];
			background_method= 		array[1];
			nb_image= 			array[2];
			rolling= 			array[3];
			pixelx= 				array[4];
			pixel10x= 			array[5];
			upper= 			array[6];
			gamma= 			array[7];
			echelle_micro=			array[8];
			process_option=		array[9];
			esdmin= 			array[10];
			esdmax= 			array[11];
			lut_offset= 			array[12];
			lut_slope= 			array[13];
			stop_after_n_images= 	array[14];
			stop_after_m_objects= 	array[15];
			scale= 				array[16];
			remove_objects_on_sides= 	array[17];
			removeduplicates= 		array[18];
			grey_auto_adjust = 		array[19];
			vignette_color= 		array[20];
			analysed_color_channel = 	array[21];
		} // if
	}
	
	// ------------------------------- MENU -----------------------------------------------------------------
	flowcam_image_options = newArray("Automatic optimisation of the width, heigth and position of the imaged area","Automatic optimisation of the position of the imaged area (if possible)","No automatic optimisation of the imaged area"," ");
	if (advanced_mode == true) {	flowcam_image_options[3] = "Reprocess PID and TSV from the vignettes";	}
		
	Dialog.create("Process PID and VIGNETTES            version :  "+version);
	Dialog.addMessage("--------------------------------------------------------------------------------------------------------------------------------------------------------------------      ");
	Dialog.addMessage("Instrument :  "+instrum);
	Dialog.addMessage("Project :        "+path);
	Dialog.addMessage("--------------------------------------------------------------------------------------------------------------------------------------------------------------------      ");
	Dialog.addCheckbox("BATCH process ?",true);
	Dialog.addCheckbox("SKIP processed file ?",true);
	if (instrum == "Flowcam" && indexOf(path,"flowcam_composite") < 0) {
		Dialog.addMessage("--------------------------------------------------------------------------------------------------------------------------------------------------------------------      ");
		Dialog.addMessage("The new RGB process provides RGB measurements and higher quality vignettes.\n - do not forget to set the ''upper'' to 240 \n - do not use the RGB method if the previous samples have been processed with the original method (or reprocess all)");
		Dialog.addCheckbox("Use the new process (RGB images only) ?",true);
		Dialog.addMessage("--------------------------------------------------------------------------------------------------------------------------------------------------------------------      ");
		Dialog.addMessage("The 1st automatically optimises the width and height of the imaged area beetween the detected limits of the cell (recommended).\nThe 2nd automaticall centers the previously defined area beetween the limits of the cell (if possible).\nThe 3rd option checks each sample and asks the user to accept the centering if needed and doable. ");
		Dialog.addChoice("        Select option ",flowcam_image_options);
	}	
	Dialog.addMessage("--------------------------------------------------------------------------------------------------------------------------------------------------------------------      ");
	if (indexOf(path,"flowcam_composite") < 0 ) {	Dialog.addMessage("Configuration file : "+configfile);	
		Dialog.addMessage(message);
		Dialog.addCheckbox("Modify parametres ?",false);
	}
	else {	Dialog.addMessage(message);	}
	Dialog.addMessage("--------------------------------------------------------------------------------------------------------------------------------------------------------------------      ");
	Dialog.show;
	batch = 		Dialog.getCheckbox();
	skip = 			Dialog.getCheckbox();
	edit = false;
	if (instrum == "Flowcam" && indexOf(path,"flowcam_composite") < 0) {
		newRGB_process = Dialog.getCheckbox();    
		//		zone_optimisation = Dialog.getCheckbox();
		zone_choice = Dialog.getChoice();
		zone_optimisation = 2;
		if (zone_choice == flowcam_image_options[0]) { zone_optimisation = 2;	}
		else if (zone_choice == flowcam_image_options[1]) { zone_optimisation = 1;	}
		else if (zone_choice == flowcam_image_options[2]) { zone_optimisation = 0;	}
	}
	if (indexOf(path,"flowcam_composite") <0 ) {	edit = 			Dialog.getCheckbox();	}
	
	// ----------------------- WARNING for RGB proces ----------------
	change_upper = false;
	if (indexOf(path,"flowcam_composite") < 0 ) {
		if (parseInt(upper) != 240 && newRGB_process == true) {
			change_upper = getBoolean("You selected the new RGB image process but the ''upper'' segmentation threshold is "+upper+" instead of 240. \n \nPress YES to edit the configuration and change this value to 240 using the ''Edit configuration tool''. \n \nPress NO to continue at your own risk with "+upper+".");
		}
	}
	
	// ------------------------------------------ Edit configuration -------------------------------------
	if (edit == true || change_upper == true ) {
		ret = runMacro("Zooprocess_edit_configuration",pathconfig+" "+instrum+" edit "+path);
		flag=toString(ret);
		l= lengthOf(flag);
		if (l==1)  { 	showMessage("WARNING","Error message : process aborted.                       "); 	} // if
	} // if
} // while edit
if (indexOf(path,"flowcam_composite") > 0 ) {
	// -------------- FLOWCAM composite -----------------
	calibration = 			"no";
	background_method= 		"no";
	nb_image= 			5;
	rolling= 			100;
	pixel4x= 			1.29;
	pixel10x= 			5;
	upper= 				245;
	gamma= 				2;
	echelle_micro=			100;
	process_option=			"advanced";
	esdmin= 			40;
	esdmax= 			1000000;
	lut_offset= 			0;
	lut_slope= 			1;
	stop_after_n_images= 		10000;
	stop_after_m_objects= 		10000;
	scale= 				2;
	remove_objects_on_sides= 	"yes";
}

// ==================================== FLOWCAM & Generic PROCESS ====================================================
// ---------------------- Ouverture META ---------------------------------------------------------------
open(path+"\\meta\\"+metafile);
wait(100);
metadata = getInfo();
run('Close');
if (indexOf(metadata,";") < 0 ) {	aa = getBoolean("The "+metafile+" has been modified outside of Zooprocess and is now TAB separated instead of dot-coma. \n \nPress Cancel to abort. \nCorrect your file\nRestart the process !");	}
array = split(metadata,"\n");

// ------------------------------------- RECOVER metadata if any work subfolder deleted ---------------------------
recover = false;
for (i=1; i<array.length ; i++) { 
	// ----------------- Nom de fichier ---------------------------------------
	ligne = replace(array[i],";"," ");
	ligne = split(ligne,"\ ");
	Project = 	ligne[0];
	Filename = 	ligne[2];
	SampleId =	ligne[3];
	pathprofile = path+"\\work\\"+SampleId+"\\";
	pathprofilemeta  = pathprofile+SampleId+"_meta.txt";
	if (File.exists(pathprofile) == false || File.exists(pathprofilemeta) == false ) {
		// ----------------- Impression de la premiere lignes -----------------
		if (isOpen("Log")) {	selectWindow("Log");	run("Close");	} // if
		print(array[0]);	
		// ----------------- Impression de la ligne du profil -------------------	
		print(array[i]);	
		// ---------------- Creation repertoire profil -------------------------------------------------
		File.makeDirectory(pathprofile);
		// ---------------- Sauvegarde fichier individuel dans repertoire profil ---------------
		sauve = "save=" + pathprofile+SampleId+"_meta.txt";
		selectWindow("Log");
		run("Text...",sauve);
		run('Close');
		recover = true;
	} //  if
} // for

// ------------------------------- Liste des profils traitables --------------------------------------------------------
list = 	newArray(array.length);
j = 0;
// showMessage("Zooprocess is listing the samples to be processed. \n \nPlease wait for the next screen indicating this information !");
process_checks = getBoolean("----------------------------------------------------------------------------\nTESTING OPTION : \n----------------------------------------------------------------------------\n \nDo you want to perform a complete test of the profiles to process ? \n \nThis test permits to detect the missing files and provides a log of the task. \nThe process is time demanding. \nThe process does not check the imaging dimensions. \n \nPress YES to proceed \nPress NO to skip the test at your own risks for the precessing of the samples.");

if (indexOf(path,"flowcam_composite") < 0  && indexOf(path,"generic") < 0) {
	// -------------------- Autre que FlowCam composite et autre que generic --------------
	if (isOpen("Log")) {	selectWindow("Log");		run('Close');	}
	File.makeDirectory(path+"\\logs\\");
	warning_text = "----------------------------------------------------------------------------\nWARNINGS on the samples to process : \n----------------------------------------------------------------------------\n ";
	warning_text_tsv = "----------------------------------------------------------------------------\nWARNINGS for PID files without ecotaxa*.tsv file : \n----------------------------------------------------------------------------\n ";
	// ------------------------ Starting time -----------------------------------------------
	dateheurestart =	runMacro("Zooscan_get_time_5");
	
	// ---------------------- Liste des raw folders -------------------		
	raw_folders  = getFileList(pathraw);
	nbfolders_raw = raw_folders.length;	
	nbfolders = array.length - 1;
	
	if (process_checks == true) {	
		print('-------------------------- START ---------------------------------------------');
		print(nbfolders + " samples in the "+path+"\\meta\\"+metafile +" file.");
		print(nbfolders_raw + " raw folders in "+path+"\\raw\\ are being checked.");	
		if (nbfolders_raw != nbfolders) { 
			warning_text = warning_text +"\nAll sample metadata not filled in "+path+"\\meta\\"+metafile+"\n ";	
			print("All sample metadata not filled in "+path+"\\meta\\"+metafile);
		}
//		print('------------------------------------------------------------');
	}
	comas = false;
	for (m = 1; m < array.length ; m++) { 
		ligne = replace(array[m],";"," ");
		ligne = split(ligne,"\ ");
		Project = 		ligne[0];
		Filename = 		ligne[2];
		SampleId =		ligne[3];
		
		Calibration_Factor = 9999;
		sum_file = 0;	
		
		// ------------------------------ PROGRESS -----------------------------------------
		progress = m / (nbfolders);
		showProgress(progress);		showStatus("Checking "+SampleId);
		
		// ------------------------------ Existence du BACK selon methode -----------------------------------------------------
		testback = true;
		pathback = path+"\\back\\"+instrum+"_"+SampleId+"\\"+SampleId+"_back.tif";
		if (background_method == "average_image") {		testback = File.exists(pathback);		} // if
		
		// ------------------------------ Existence du META -----------------------------------------------------
		profileraw = "flowcam_"+Filename;
		pathmeta = path+"\\work\\"+SampleId+"\\"+SampleId+"_meta.txt";
		source = "nok";
		if( testback == true &&  File.exists(pathmeta) == true  &&   File.exists(pathraw+instrum+"_"+Filename)== true    )  source = "ok";
		
		// ------------------------- Vérification des fichiers CTX et summary -------------------
		raw_file = true;
		if (process_checks == true) {
			ctx_file = 0;
			capture_bottom = 9999;
			capture_left = 9999;
			capture_right = 9999;
			capture_top = 9999;
			capture_pixel_border = 9999;		
			// ------------------ Impression ----------------------------------------------
			print("--------------------"+ m +" / " + nbfolders +"-------------------------------------------------");
			print("Filename= "+Filename);
			print("SampleId= "+SampleId);
			// ------------------ Liste des fichiers du répertoire RAW --------------------
			filelist_raw  = getFileList(pathraw + profileraw);			
			if (filelist_raw.length == 0) {	
				print("No file/folder "+pathraw + profileraw);
				warning_text = warning_text + "\n" + Filename + ": no file ";
			}
			else {
				// --------------- Présence d'au moins un fichier -------------------------
				list_raw = 	newArray(filelist_raw.length);	
				search_ctx_sum = true;
				ctx = false;
				sum = false;
				k = 0;
				while (search_ctx_sum == true) {
//				for (k = 1; k < filelist_raw.length; k++) {					
					if ((endsWith(filelist_raw[k],"_run_summary.txt") == true || endsWith(filelist_raw[k],"summary.csv")) && sum_file == 0)	{
						// ------------------- Vérification de la présence de "Total" ------------------
						open (pathraw+profileraw+"\\"+filelist_raw[k]);
						wait(100);
						summary = getInfo();
						run("Close");
						if (indexOf(summary,",,,") >0) {	warning_text = warning_text + "\n" + SampleId+": "+ filelist_raw[k] + " contains series of ,,,,,, and '' to be cleaned before processing ! ";	
							print(filelist_raw[k] +" contains ,,,,,, and '' to be cleaned using TextCrawler or NotePad+ before processing !");
							comas = true;
							summary = replace(summary,",,,,,","");
							summary = replace(summary,",,,,","");
							summary = replace(summary,",,,","");
							summary = replace(summary,",,","");
						}
						else if (indexOf(summary,"Total") > 0 ) {
							conf  = split(summary,"\n");	
							for (i=0; i<conf.length; i++) {
								text = toString(conf[i]);
								if (indexOf(toLowerCase(text),"total") > 0 && indexOf(toLowerCase(text),"total") < 3) {
									warning_text = warning_text + "\n" + SampleId+": "+ filelist_raw[k] + " may contain '' or tabs, process possible !";
									print(filelist_raw[k] +" may contain '' or tabs, process possible !");
								}
								if (indexOf(text,"Magnification") > -1 ) {
									aa = replace(text,":"," ");
									aa = replace(aa,","," ");
									aa = replace(aa,"="," ");
									bb = split(aa,"\ ");
									lenstype = bb[bb.length -1];
//									print("Lenstype = "+lenstype);
								} // if
								if (indexOf(text,"Calibration Factor") > -1) {		
									aa = replace(text,":"," ");
									aa = replace(aa,","," ");
									aa = replace(aa,"="," ");
									bb = split(aa,"\ ");
									Calibration_Factor = bb[bb.length -1];
									//									print("Calibration_Factor = "+Calibration_Factor);
								} // if
							} // for
							// -------------------------- Vérification Calibration factor -----------------------
							aa = 999*Calibration_Factor;
							if (isNaN(aa)) { 
								print("The summary file "+filelist_raw[k]+" must contain some ''. CLEAN IT !");
								warning_text = warning_text + "\n" + filelist_raw[k] + " : contains some '' and must be cleaned.";	
							}
							else {	sum_file++;
								print("Summary file OK : "+filelist_raw[k]);	
								sum = true;
							}
						} // if
					}
					if (endsWith(filelist_raw[k],".ctx") == true )	{ 
						// ------------- Vérification de la présence des bonnes données ---------
						// ---------------------- Lecture CTX ----------------------------------
						open (pathraw+profileraw+"\\"+filelist_raw[k]);
						wait(100);
						ctx_settings = getInfo();
						run("Close");
						
						// ----------------------- Lecture des valeurs de CROP utilisées par VS --------------------
						ctx_data = split(ctx_settings,"\n");
						for (i=0; i < ctx_data.length; i++) {						
							aa = split(ctx_data[i],"=");
							if (aa.length == 2) {
								if (aa[0] == "AcceptableBottom") {		capture_bottom = parseInt(aa[1]);}	//print(aa[0]+"--"+aa[1]);
								if (aa[0] == "AcceptableLeft") 	{		capture_left = parseInt(aa[1]);}
								if (aa[0] == "AcceptableRight") {		capture_right = parseInt(aa[1]);}
								if (aa[0] == "AcceptableTop") 	{		capture_top = parseInt(aa[1]);}
								if (aa[0] == "ImagePixelBorder") {		capture_pixel_border = parseInt(aa[1]);	}
								if (aa[0] == "CalibrationConstant") {		Calibration_Factor = parseInt(aa[1]);	}
							}
						}
						if (capture_bottom+capture_left+capture_right+capture_top+capture_pixel_border > 10000) {
							print("CTX file NOK : "+filelist_raw[k]);
							warning_text = warning_text + "\n" + filelist_raw[k] + " : does not contain the necessary parameters";
						}
						else {	ctx_file++;	
							print("CTX file OK : "+filelist_raw[k]);	
							ctx = true;
						}
					} // if
					k++;
	
					if (sum == true && ctx == true && Calibration_Factor < 100) {	search_ctx_sum = false;	}
					if (k == filelist_raw.length) {					search_ctx_sum = false;	}
				}
				
				if ( sum == false )	{ print("NO valid Summary file");	}
				
				// ---------------------------- Calibration factor ------------------------
				raw_file = false;
				if (ctx_file == 0) {
					print("The CTX file has not been exported from Visual Spreadsheet");
					warning_text = warning_text + "\n" + SampleId + " : the CTX file has not been exported from Visual Spreadsheet";
				}
				else if (Calibration_Factor > 100 ){	print("The Calibration factor is not read in the CTX file or in the summary file. ");
					warning_text = warning_text + "\n" + SampleId + " : calibration factor not read in the CTX file or in the summary file";
				}
				
				if (ctx_file == 1 && sum == true && Calibration_Factor < 100) { raw_file = true;	print( "=> "+ SampleId + " OK for processing");	}
				else if (ctx_file > 1 ) {	
					print("More than one CTX file");
					print( "=====> "+ SampleId + " NOK for processing");
					warning_text = warning_text + "\n" + SampleId + " : folder contains more than one CTX file ";				
				}
				else {	print( "=====> "+ SampleId + " NOK for processing");	}
				
				// -------------------- Enregistrement du LOG -------------------------
				sauve = "save=" + path+"\\logs\\Log_files_to_process_"+	dateheurestart+".txt";
				selectWindow("Log");
				run("Text...",sauve);
			}		
		}
		
		// ------------------------------ Existence du PID et list fichiers traitables -----------------------------------------------------
		pathpid = path+"\\work\\"+SampleId+"\\"+SampleId+"_dat1.pid";
		pathtsv = path+"\\work\\"+SampleId+"\\ecotaxa_"+SampleId+".tsv";
		pathtsv_low = path+"\\work\\"+SampleId+"\\ecotaxa_"+SampleId+"_low.tsv";
		pathtsv_high = path+"\\work\\"+SampleId+"\\ecotaxa_"+SampleId+"_high.tsv";
		
		
		if (process_checks == true && File.exists(pathpid) == true) {	print("=====> " + pathpid + " already processed");		}
		if (process_checks == true && File.exists(pathtsv) == true) {	print("=====> " + pathtsv + " already processed");		}
		if (process_checks == true && (File.exists(pathtsv_low)|| File.exists(pathtsv_high))) {	
			if (File.exists(pathtsv_low)) print("=====> " + pathtsv_low + " created");
			if (File.exists(pathtsv_high)) print("=====> " + pathtsv_high + " created");
			print("=====> " + pathtsv + " already splited");		
			warning_text = warning_text + "\n" + pathtsv + " already splited";
		}
		if( File.exists(pathpid) == false && source == "ok" && raw_file == true ) 	{	
			// PID nexiste pas mais fichiers OK dans répertoire
			list[j] = 	Filename+"  /  "+SampleId;	
			j++;			
		} // if
		else if (skip == false && source == "ok" && raw_file == true) 			{
			// Fichiers OK dans répertoire, PID indifférent car SKIP est false
			list[j] = 	Filename+"  /  "+SampleId;	
			j++;	
		} // if	
		
		// ------------------------ Check TSV ---------------------
		if (File.exists(pathpid) == true && File.exists(pathtsv) == false) {
			if (File.exists(pathtsv_low) == true || File.exists(pathtsv_high) == true) {
				warning_text_tsv = warning_text_tsv + "\n"+"ecotaxa_"+SampleId+".tsv already splited : DO NOT reprocess the sample !.";
			}
			else {	warning_text_tsv = warning_text_tsv + "\n"+"ecotaxa_"+SampleId+".tsv is not processed even if the PID exists !.";
				nb_pid_wo_tsv++;
			}
		}		
	}
	if (process_checks == true) {
		if (comas == true) {	warning_text = warning_text +"\n At least one summary file contains ,,,,, and ''\nUse an app. such as TextCrawler/Notepad++ to remove them.";	}
		if (warning_text == "INFORMATION on the samples to process : ") {	print("All raw folders can be processsed.");	}
		else {	print(" ");	print(warning_text);	}
		if (nb_pid_wo_tsv > 0) { 
			print("-------------------------------------------------------------------------");
			print(warning_text_tsv);	
		}
		print("------------------------ END --------------------------------------------------");
		// -------------------- Enregistrement du LOG -------------------------
		sauve = "save=" + path+"\\logs\\Log_files_to_process_"+	dateheurestart+".txt";
		selectWindow("Log");
		run("Text...",sauve);
	}	
}
else if (startsWith(path,"flowcam_composite")) {	
	// ----------------------- FlowCam composite -------------------------
	for (i=1; i<array.length ; i++) { 
		ligne = replace(array[i],";"," ");
		ligne = split(ligne,"\ ");
		Project = 		ligne[0];
		Filename = 		ligne[2];
		SampleId =		ligne[3];
		// ------------------- Verifier si existe données dans RAW ----------------
		ctx = File.exists(pathraw + "flowcam_"+Filename +"\\" +Filename+".ctx");
		lst = File.exists(pathraw + "flowcam_"+Filename +"\\" +Filename+".lst");
		sum = File.exists(pathraw + "flowcam_"+Filename +"\\" +Filename+"_run_summary.txt");
		imga = File.exists(pathraw + "flowcam_"+Filename +"\\"+Filename+"_images_000001.tif");
		imgb = File.exists(pathraw + "flowcam_"+Filename +"\\"+Filename+"_000001.tif");
		raw_file = false;
		if (ctx == true && lst == true && sum == true && (imga == true || imgb == true)) raw_file = true;	
		
		// ------------------- Vérifier si META et PID et TSV existent -----------------------------
		pathprofile = path+"\\work\\"+SampleId+"\\";
		meta = File.exists(pathprofile+SampleId+"_meta.txt");	
		pid = File.exists(pathprofile+SampleId+"_dat1.pid");
		eco = File.exists(pathprofile+"ecotaxa_"+SampleId+".tsv");				
		proc_file = false;
		if (meta == true && pid == true && eco == true ) proc_file = true;	
		
		// ------------------- Finalisation de la liste ---------------------------
		if (raw_file == true && skip == false ) 	{			process = true;		}
		else if (raw_file == true && skip == true && proc_file == false) {	process = true;		}
		else {									process = false;	}
		if (process == true) {	list[j] = 	Filename+"  /  "+SampleId;	j++;			}	
	}
}
// ---------------------- si PID sans TSV ------------------------
if (nb_pid_wo_tsv > 0)	{
	showMessage("----------------------------------------------------------------------------\nWARNING : \n----------------------------------------------------------------------------\n \n",nb_pid_wo_tsv + " samples have been processed correctly but the ecotaxa*.tsv files have not been created. \n \nUse the ''Create table for Ecotaxa'' tool in advanced mode \nor \nForce the reprocess of this samples.");	
}

// ----------------	Test de la presence de fichiers dans le repertoire-----------------------------------------------------------------------	
if (j==0 ) { 
	if (instrum == "flowcam" && indexOf(path,"flowcam_composite") < 0) {
		if (process_checks == true) {aa = getBoolean(warning_text+ " \n \n"+"No profiles to process in the selected project. \n \n- Check that you correctly selected the ''skip'' and ''batch'' options, \n- Check that you filled the metadata for all samples, \n- Check that the CTX files are present for all samples \n- Check that the summary files are present for all samples  \nRESTART process after checking.  \n  \nPress CANCEL now to abort !     ");}
		else {	aa = getBoolean("TEST RESULTS : \n \nNo profiles to process in the selected project. \n \n- Check that you correctly selected the ''skip'' and ''batch'' options, \n- Check that you filled the metadata for all samples, \n- Check that the CTX files are present for all samples \n- Check that the summary files are present for all samples  \nRESTART process after checking.  \n  \nPress CANCEL now to abort !     ");}
	} // if
	else if (instrum == "generic") {aa = getBoolean("TEST RESULTS : \n \nNo profiles to process in the selected project. \n\n- Check that you filled the metadata for all samples, \n \nPress CANCEL to abort !          ");	} // if
	else {	aa = getBoolean("TEST RESULTS : \n \nNo profiles to process in the selected project. \n\n- Check that you filled the metadata for all samples, \n- Check that all files in the raw folders starts with ''flowcam_'' \n \nPress CANCEL to abort !          ");	}
} //
else { 	listproc = newArray(j);
	if (process_checks == true) {		aa = getBoolean(warning_text+ " \n \n"+toString(j)+ " samples are complete and can be processed. \n---------------------------------------------------------------------------- \nYou can check the logfile : \n"+path+"\\logs\\Log_files_to_process_"+dateheurestart+".txt"+"\n \nPress YES to Continue. \nPress Cancel to abort");	}
	for (k=0;k<listproc.length ;k++) {	rr = list[k];	listproc[k] = rr;	} // for
} // else

// -------------------------------------------- PID et Vignettes --------------------------------------------------------------------------------------------	
if (zone_choice == flowcam_image_options[3]) {
//	aa = getBoolean("There is no control of the already processed files. \n \nIn case of BATCH process, please remove the already ''reprocessed'' samples from the ''work'' folder before processing.");
}

if (batch == false) {
	// ---------------------------- SELECT PROFILE ---------------------------------
	Dialog.create("Process PID and Vignettes            version :  "+version);
	Dialog.addMessage("--------------------------------------------------------------------------------------------------------------------------------------      ");
	Dialog.addMessage("Instrument :  "+instrum);
	Dialog.addMessage("Project :        "+path);
	Dialog.addMessage("--------------------------------------------------------------------------------------------------------------------------------------      ");
	if (instrum == "Flowcam" && newRGB_process == true) Dialog.addMessage("The new RGB process is selected.");
	Dialog.addMessage("SELECT PROFILE to PROCESS (Filename  /  SampleId)");
	Dialog.addChoice("               ",       listproc         );
	Dialog.addMessage("--------------------------------------------------------------------------------------------------------------------------------------      ");
	Dialog.show;
	profileprocessed = 		Dialog.getChoice();
	profileprocessed = replace(profileprocessed,"  /  "," ");
	aa = split(profileprocessed," ");
	Filename = aa[0];
	SampleId = aa[1];
	
	// ------------------------ PID ----------------------------------------------------------------------------------------------
	if (instrum == "Flowcam" && newRGB_process == false && zone_choice == flowcam_image_options[3])	{	ret = runMacro("Zooprocess_flowcam_PID_from_Vignettes", path+" "+SampleId+" "+Filename);	}
	else if (indexOf(path,"flowcam_composite") > 0)			{	ret = runMacro("Zooprocess_flowcam_PID_Vignettes_composite",path+" "+Filename+" "+configfile+" "+instrum+" "+SampleId);	}
	else if (instrum == "Flowcam" && newRGB_process == false) 	{	ret = runMacro("Zooprocess_flowcam_PID_Vignettes_bw",path+" "+Filename+" "+configfile+" "+instrum+" "+SampleId+" "+zone_optimisation);		}
	else if (instrum == "Flowcam" && newRGB_process == true) 	{	ret = runMacro("Zooprocess_flowcam_PID_Vignettes_color_2",path+" "+Filename+" "+configfile+" "+instrum+" "+SampleId+" "+zone_optimisation);	}
	else if (instrum == "Generic") 					{	ret = runMacro("Zooprocess_generic_PID_Vignettes",path+" "+Filename+" "+configfile+" "+instrum);			}
	flag=toString(ret);
	if (lengthOf(flag) == 1)  { 	showMessage("WARNING","PID process aborted.                       "); 	} // if
} // if
else {	// --------------- Batch process ----------------------
	for (k=0;k<listproc.length ;k++) {	
		// ------------- Test presence RUN_file ---------------------------
		if (File.exists(text_run)) { 
			aa = replace(listproc[k]," / "," ");
			aa = split(aa," ");
			Filename = aa[0];
			SampleId = aa[1];
			// ------------------------ PID ----------------------------------------------------------------------------------------------
			exist_pid = File.exists(path+"\\work\\"+SampleId+"\\"+SampleId+"_dat1.pid");
			if (instrum == "Flowcam" && newRGB_process == false && zone_choice == flowcam_image_options[3] && exist_pid == true)	{	ret = runMacro("Zooprocess_flowcam_PID_from_Vignettes", path+" "+SampleId+" "+Filename);	}
			else if (indexOf(path,"flowcam_composite") > 0)			{	ret = runMacro("Zooprocess_flowcam_PID_Vignettes_composite",path+" "+Filename+" "+configfile+" "+instrum+" "+SampleId);	}
			else if (instrum == "Flowcam" && newRGB_process == false) 	{	ret = runMacro("Zooprocess_flowcam_PID_Vignettes_bw",path+" "+Filename+" "+configfile+" "+instrum+" "+SampleId+" "+zone_optimisation);		}
			else if (instrum == "Flowcam" && newRGB_process == true) 	{	ret = runMacro("Zooprocess_flowcam_PID_Vignettes_color_2",path+" "+Filename+" "+configfile+" "+instrum+" "+SampleId+" "+zone_optimisation);	}
			else if (instrum == "Generic") 					{	ret = runMacro("Zooprocess_generic_PID_Vignettes",path+" "+Filename+" "+configfile+" "+instrum);			}
			flag=toString(ret);
			if (lengthOf(flag) == 1)  { 	showMessage("WARNING","Error message PID process aborted.                       "); 	} // if	
		} // if
//		else { showMessage("Process stopped by operator !");	} // else
	} // for
} // else

// ----------------------------- FIN ----------------------------------------------
if (File.exists(text_stop) == true) {  run_stop = File.rename(text_stop,text_run);	} // if
ret = "OK";
return ret;
