//---------------------------Zooscan_view_image_outlines.txt---------------------------------
// Permet de voir ce qui est reellement pris en compte sur les organismes
// Utilise le OUT et le VIS
version = "3.11";
date = "2007/06/13";
// --------------Cette routine ne fonctionne que dans le respect de l'arborescence Zooscan_scan\_work\
args = getArgument();
arg = split(args," ");
chem = 		arg[0];
projfolder = 	arg[1];
chemconfig = 	projfolder + "\\Zooscan_config\\";
chemscan = 	projfolder  + "\\Zooscan_scan\\";
chemresults = 	projfolder  + "\\Zooscan_results\\";
chemwork = 	chemscan + "_work\\";
chemtemp = 	chem + "Zooscan_temp\\";

lon = lengthOf(projfolder);
zoosc = indexOf(projfolder,"Zooscan_");
proj = substring(projfolder,zoosc+8,lon);

run("Input/Output...", "jpeg=100 file=.xls");
resolution = "2400";
contrast = 1.5;

//--------------------Ouverture controlee du VIS et du PID -----------------------
check = 0;
while (check == 0) {
	//---------------------------- Recherche de dossiers ''complets'' dans le repertoire "work" ----------------------------
	workfolder = 	File.exists(chemwork);	
	if (workfolder == 1) {			
		filelist  = getFileList(chemwork);
		//-------------- Selection des fichiers ---------------------
		j = 0;	flagframe = 0;
		list = newArray(filelist.length);
		for (i=0; i<filelist.length ; i++) {	  
			 rr = filelist[i]; 
			identlong = 	lengthOf(rr);
			rr = 		substring(rr,0,identlong-1);	
			//--------------- Recherche du fichier image en ZIP ou en JPG ----------
			imagezip  =  chemwork+"\\"+rr+"\\"+rr+"_vis1.zip";
			imagejpg = chemwork+"\\"+rr+"\\"+rr+"_vis1.jpg";
			imagelog = chemwork+"\\"+rr+"\\"+rr+"_log.txt";
			imagepid = chemwork+"\\"+rr+"\\"+rr+"_dat1.pid";
			imagemsk = chemwork+"\\"+rr+"\\"+rr+"_msk1.gif";
			imageout = chemwork+"\\"+rr+"\\"+rr+"_out1.gif";
			zipexist = File.exists(imagezip);
			jpgexist = File.exists(imagejpg);
			logexist = File.exists(imagelog);
			pidexist = File.exists(imagepid);
			mskexist = File.exists(imagemsk);
			outexist = File.exists(imageout);
			if ((zipexist == 1 || jpgexist == 1)&&logexist == 1 && pidexist == 1 && mskexist == 1 && outexist == 1){ list[j] = rr;  j++;  	flagframe = 0; 		} // if
		} // for
		if (j==0) { print("No valid ''sample'' folder in the "+projfolder);
			configpres = 0;
			getBoolean("No valid sample folder in "+projfolder+"             \nPress CANCEL to ABORT !                 ");
		} //
		else {	// ---------------------------Purger les lignes vides  ----------------------
			listaff = newArray(j);
			for (i=0; i<listaff.length ; i++) {	rr = list[i]; 	
				//	identlong = 	lengthOf(rr);
				//	rr = 	substring(rr,0,identlong-1);	
				listaff[i] = rr;	
			} // for	
			// -------------------------- Selection de l'image a traiter ----------------------------
			Dialog.create("VIEW image with outlines   v : "+version);
			Dialog.addMessage( "-------------------------------------------------------------------------------");
			Dialog.addMessage( "Project :  "+proj);
			Dialog.addMessage( "-------------------------------------------------------------------------------");
			Dialog.addMessage("- If gamma > 1=> contrast enhanced              ");
			Dialog.addMessage("- If gamma < 1=> image smoothed              ");
			Dialog.addMessage("- If gamma = 1=> No gamma correction              ");
			Dialog.addNumber("                      SET gamma (0.5 - 5) ",contrast);
			Dialog.addCheckbox("CLEAR outside outlines ?", false);
			Dialog.addCheckbox("SAVE image with outlines ?", false);
			Dialog.addMessage( "-------------------------------------------------------------------------------");
			Dialog.addMessage("SELECT Image from the project            ");
			Dialog.addChoice("    ", listaff);
			Dialog.addMessage( "-------------------------------------------------------------------------------");
			Dialog.show();
			gamma =		Dialog.getNumber();
			gamma = 	maxOf(gamma,0.5);
			gamma = 	minOf(gamma,5);
			maskout = 	1;
			maskclear = 	Dialog.getCheckbox();
			masksave = 	Dialog.getCheckbox();
			titrered = 	Dialog.getChoice();
			check = 1;
		}// else
	} // if
	else { getBoolean("No "+chemwork+" folder, press CANCEL to ABORT !");		} // else
} // while

print("Sample = "+titrered);

//------------------- Fermeture d'eventuelles images ----------------------------------------
while (nImages()>0) {selectImage(nImages());  	run("Close");	} // while	
run("free memory");		wait(500);	
run("free memory");		wait(500);	
run("free memory");		wait(500);	
run("free memory");		wait(500);

chemwork1 = chemwork + "\\"+titrered+"\\";

text = 		chemwork1+titrered+"_vis1.jpg";
exist = 		File.exists(text);
textzip = 		chemwork1+titrered+"_vis1.zip";
existzip = 	File.exists(textzip);
imageref = titrered;	
if (existzip == 1) {	open(textzip);	
	titre = 	titrered+"_vis1.tif";
} // if
else { 	open(text);
	titre = 	titrered+"_vis1.jpg";
	openimg = 1;
} // else
saveAs("Tiff",chemtemp+"temp_vis1.tif");
close();
run("free memory");
wait(1000);
open(chemtemp+"temp_vis1.tif");
rename(titrered);
selectImage(titrered);
setVoxelSize(1,1,1,"pixel");
//---------------------CLEAR BACKGROUND -----------------------------------------
if (maskclear == 1) {	// --------------------- Ouverturte MSK1------------------------------------------
	openfile = chemwork1+ titrered+"_msk1.gif";
	print("Clear background");
	open(openfile);
	test = 1;	
	rename("MASK");
	text =  "image1=MASK operation=Add image2="+titrered;
	run("Image Calculator...", text);
	if (isOpen(titrered)) 	{	selectImage(titrered);	
		close();		
		saveAs("Tiff",chemtemp+"temp_vis1.tif");
		close();
		run("free memory");		wait(500);	
		run("free memory");		wait(500);	
		run("free memory");		wait(500);	
		run("free memory");		wait(500);
		open(chemtemp+"temp_vis1.tif");
		rename(titrered);
		selectImage(titrered);
		setVoxelSize(1,1,1,"pixel");
	} // if	
	else { message = "No msk1.gif  file available for the selected image.              " ;	print(message);		} // else	
} // if

//---------------------Ouverture des outlines -----------------------------------------
if (maskout == 1) {	// --------------------- Ouverture outlines------------------------------------------
	openfile = chemwork1+ titrered+"_out1.gif";
	open(openfile);	
	test = 1;		
	rename("Contours");	
	selectImage(titrered);
	run("Invert");
	text =  "image1=Contours operation=Add image2="+titrered;
	run("Image Calculator...", text);
	selectImage(titrered);	
	close();	
	run("Invert");
	rename(titrered);
	selectImage(titrered);
	setVoxelSize(1,1,1,"pixel");
} // if

//---------------------Rehaussement du contraste-----------------
selectImage(titrered);
textgamma = 	"value="+contrast;	
run("Gamma...", textgamma);	

// -------------------- Sauvegarde --------------------------------------------------------------------------
if (masksave == true) { 	saveAs("zip",chemwork1+titrered+"_zoocont");		} // if
else { 	saveAs("tif",chemtemp+"temp_vis1");					} // else

ret = titre;
return ret;
